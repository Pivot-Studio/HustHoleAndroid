if (isRelease) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion androidID.compileSdkVersion
    defaultConfig {
        minSdkVersion androidID.minSdkVersion
        targetSdkVersion androidID.targetSdkVersion
        versionCode androidID.versionCode
        versionName androidID.versionName

        if (!isRelease) {//当为release模式时不需要id
            applicationId "cn.pivotstudio.modulep.hole"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))

        javaCompileOptions {//ARouter跨组件需要
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        dataBinding {//
            enabled = true
        }

        buildFeatures {
            viewBinding true
            compose true
        }

        composeOptions {
            kotlinCompilerExtensionVersion = "1.5.0"
        }
    }

    sourceSets {
        if (!isMac) {
            main {
                if (!isRelease) {//不同状态下使用不同清单文件
                    manifest.srcFile '/src/main/debug/AndroidManifest.xml'
                } else {
                    manifest.srcFile '/src/main/AndroidManifest.xml'
                    java {
                        //release时所有debug目录下的文件不合并
                        exclude '**/debug/**'
                    }
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
    namespace 'cn.pivotstudio.modulep.hole'
}

dependencies {
    implementation project(':modulesBase:resBase')

    // Common Dependencies
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    kapt "com.alibaba:arouter-compiler:$ARouter_version"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    // Markwon (Markdown)
    implementation "ru.noties:markwon:$markdown_version"
    implementation "ru.noties:markwon-image-loader:$markdown_version"
    implementation "ru.noties:markwon-view:$markdown_version"

    // Room Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Material
    implementation "com.google.android.material:material:$material_version"

    // Compose
    implementation "androidx.compose.material3:material3:$material3_version"
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-tooling"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
}
