if (isRelease) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion androidID.compileSdkVersion
    defaultConfig {
        minSdkVersion androidID.minSdkVersion
        targetSdkVersion androidID.targetSdkVersion
        versionCode androidID.versionCode
        versionName androidID.versionName

        if (!isRelease) {//当为release模式时不需要id
            applicationId "cn.pivotstudio.modulep.hole"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))

        javaCompileOptions {//ARouter跨组件需要
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        dataBinding {//
            enabled = true
        }
    }

    sourceSets {
        if (!isMac) {
            main {
                if (!isRelease) {//不同状态下使用不同清单文件
                    manifest.srcFile '/src/main/debug/AndroidManifest.xml'
                } else {
                    manifest.srcFile '/src/main/AndroidManifest.xml'
                    java {
                        //release时所有debug目录下的文件不合并
                        exclude '**/debug/**'
                    }
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'cn.pivotstudio.modulep.hole'
}

dependencies {
    implementation project(':modulesBase:libBase')
    implementation project(':modulesBase:networkBase')
    implementation project(':modulesBase:dataBase')
    implementation project(':modulesBase:resBase')
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation  "ru.noties:markwon:$markdown_version"//markdown
    implementation "ru.noties:markwon-image-loader:$markdown_version" // optional
    implementation "ru.noties:markwon-view:$markdown_version" // optional

    //Room数据库
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    annotationProcessor "com.alibaba:arouter-compiler:$ARouter_version"//每个组件都要添加

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}